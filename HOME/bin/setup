#!/usr/bin/env just --justfile
list:
	@just --list --justfile {{justfile()}}

vendor := "~/3rdparty"
setup-path := canonicalize(justfile() / "../../..") # HOME/bin/setup
conf := setup-path / "conf"

wd:
	@pwd

vendor:
	@echo {{vendor}}

bin:
	@echo {{setup-path}}/HOME/bin

path:
	@echo {{setup-path}}

conf:
	@echo {{conf}}

brew:
	#!/usr/bin/env zsh
	set -Eeuxo pipefail

	# run everything for homebrew
	homebrew-workflow {{conf}}/Brewfile

	# update to homebrew'd shell
	update-shell "$(brew --prefix)/bin/zsh"

	# cache zsh plugins
	# note: 3rdparty should sort and therefore be sourced first in .zshrc, while
	#       ~3rdparty should sort and be sourced last.
	plugins=~/bin/shell/3rdparty/_plugins.zsh
	direnv hook zsh > $plugins
	zoxide init zsh >> $plugins
	fzf --zsh >> $plugins
	atuin init zsh >> $plugins
	broot --print-shell-function zsh >> $plugins
	broot --set-install-state installed

	link(){ ln -sf "$(brew --prefix)/share/$1/$1.zsh" ~/bin/shell/~3rdparty/$1.zsh; }
	link zsh-autosuggestions
	link zsh-syntax-highlighting

	# delete cached brew script so it's regenerated
	if [[ -n "$BREW_SHELLENV_PATH" && -f "$BREW_SHELLENV_PATH" ]]; then
		rm $BREW_SHELLENV_PATH
	fi

python:
	uv venv ~/bin/.venv
	uv pip install --strict --python ~/bin/.venv/bin/python -r {{conf}}/requirements.txt

python-tools:
	cat {{conf}}/pipx.txt | xargs -t -L1 uv tool install
	uv tool upgrade --all

node:
	cat {{conf}}/npm.txt | xargs -t npm install -g

go:
	cat {{conf}}/go.txt | xargs -t -L1 go install

vscode:
	install-vscode-extensions {{conf}}/vscode.txt

manual:
	install-manual {{conf}}/manual.toml {{vendor}}

symlinks:
	symgr {{setup-path}}/HOME ~

rust:
	#!/usr/bin/env bash
	set -Eeuxo pipefail

	rustup-init -y --no-modify-path;
	source $HOME/.cargo/env;
	rustup update;
	rustup install nightly;

cargo:
	cat {{conf}}/cargo.txt | xargs -t cargo install

mac:
	PYTHONPATH=~/bin ~/bin/.venv/bin/python {{conf}}/mac.py

# Restart Finder, Menubar, Dock, etc.
restartservices:
	PYTHONPATH=~/bin ~/bin/.venv/bin/python -c "import lib.mac; lib.mac.restart_os_functions()"

# Open the setup directory in your editor
edit:
	code {{setup-path}}

# Update the setup repository
pull:
	cd {{setup-path}} && git pull

# Install all software packages
packages: python python-tools node go rust cargo vscode

bootstrap:
	mkdir -p ~/bin/shell/{,~}3rdparty/

# The full set of commands used on first setup
init: bootstrap brew packages manual symlinks mac restartservices

# One-stop shopping to update setup repo and most things
update: pull brew packages symlinks

# 'Bless' files
bless path:
	#!/usr/bin/env zsh
	cd "{{invocation_directory()}}"
	source_path="$(grealpath "{{path}}")"
	controlled_path="$(grealpath --relative-to="$HOME" "$source_path")"
	symgr --bless "$source_path" "{{setup-path}}/HOME/$controlled_path"
