#!/usr/bin/env nu
def find-tests [] {(
  rg '^func\s+(Test[A-Z0-9]\w*)'
    -or '$1'
    --no-heading
    --with-filename
    --no-line-number
    --sort=path
    --color=always
)}

def select-test [] {
  # searches codebase for tests and lets user fuzzy find them
  # returns [{path:, name:}, ...] for selected tests
  find-tests | fzf --ansi | lines | split column : path name
}

def go-test-cmd [testpath funcname --verbose(-v)] {
  let parentdir = ($testpath | path parse).parent
  mut args = [test $"'./($parentdir)'" -run $"'^($funcname)$'"]
  if $verbose {
    $args = $args ++ ['-v']
  }
  [go $args]
}

def dlv-debug-cmd [testpath funcname --verbose(-v)] {
  let parentdir = ($testpath | path parse).parent
  # todo: use temp file for delve-init so that this script is self-contained
  mut args = [test $"'./($parentdir)'" --init ~/setup/snip/delve-init -- -test.run $"'^($funcname)$'"]
  if $verbose {
    $args = $args ++ ['-test.v']
  }
  [dlv $args]
}

def run [cmd] {
  # cmd is a list of [command, args]
  $cmd | flatten | str join ' ' # show command
  run-external $cmd.0 $cmd.1
}

def main [
  --debug (-d)
  --print (-p)
  --all (-a)
  --verbose (-v)
] {
  if $all {
    if $debug {
      run [dlv [test]] # I don't see an equivalent to run all tests in dlv
    } else {
      mut args = [test './...']
      if $verbose {
        $args = $args ++ ['-v']
      }
      run [go $args]
    }
    return
  }

  let test = select-test
  if ($test | length) == 0 {
    exit 0 # no test selected
  }
  if $debug {
    run (dlv-debug-cmd $test.path.0 $test.name.0 --verbose)
  } else {
    run (go-test-cmd $test.path.0 $test.name.0 --verbose)
  }
}
